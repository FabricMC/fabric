plugins {
	id 'java'
	id 'eclipse'
	id 'idea'
	id 'maven-publish'
	id 'fabric-loom' version '0.2.5-SNAPSHOT' apply false
	id 'net.minecrell.licenser' version '0.4.1'	
	id "org.ajoberstar.grgit" version "3.1.1"
	id 'com.matthewprenger.cursegradle' version "1.1.2"
}

def ENV = System.getenv()

class Globals {
	static def baseVersion = "0.3.0"
	static def mcVersion = "1.14.4"
	static def yarnVersion = "+build.1"
}

import org.apache.commons.codec.digest.DigestUtils
import groovy.json.JsonSlurper
import groovy.json.JsonOutput

@SuppressWarnings("unused")
def getSubprojectVersion(project, version) {
	if (version.count(".") != 1) {
		throw new Exception("${project.name}'s version must only be major.minor")
	}
	if (grgit == null) {
		return version + "+nogit"
	}
	def latestCommits = grgit.log(paths: [project.name], maxCommits: 1)
	if (latestCommits.isEmpty()) {
		return version + "+uncommited"
	} else {
		def commitHash = latestCommits.get(0).id
		def buildGradleHash = DigestUtils.sha256Hex(file("build.gradle").text)

		//TODO once we branch out to 1.15 we need to talk about the last parameter
		def patch = getPatch(project, commitHash + buildGradleHash, Globals.mcVersion)

		return "${version}.${patch}+${Globals.mcVersion}"
	}
}

def getPatch(gradleProject, hash, b) {
	def jsonSlurper = new JsonSlurper()
	def jsonFile = new File(gradleProject.gradle.gradleUserHomeDir, "fabric_patch_numbers.json")
	def json = [:]
	if(jsonFile.exists()){
		json = jsonSlurper.parseText(jsonFile.text)
	}

	def project = json[gradleProject.name]
	if(project == null){
		project = [:]
	}

	def branch = project[b]
	if(!branch){
		if(project.size() == 0){
			//No existing branches found we start from fresh
			branch = [:]
		} else {
			int patchNumber

			//Finds the branch with the largest patch number to "fork" from
			project.each {branches ->
				branches.value.each {pb ->
					if(pb.value.patch > patchNumber){
						branch = cloneJson(branches.value)
						patchNumber = pb.value.patch
					}
				}
			}
		}

		if(branch == null){
			throw new Exception("failed to find branch, something bad happened.")
		}

		project[b] = branch
	}

	int patchNumber

	if(branch[hash]){
		//Returns the existing patch number
		println(branch[hash])
		patchNumber = branch[hash].patch
	} else {
		//Finds the next patch number, and stores it along with the input hash
		branch.each {
			if (it.value.patch > patchNumber){
				patchNumber = it.value.patch
			}
		}
		patchNumber++
		branch[hash] = [
		    patch: patchNumber
		]
	}

	//write out the updated json file
	json[gradleProject.name] = project
	jsonFile.write(JsonOutput.prettyPrint(JsonOutput.toJson(json)))

	return patchNumber
}

def cloneJson(input) {
	//Awful code but seemed the easiest way to do this
	return new JsonSlurper().parseText(JsonOutput.toJson(input))
}

allprojects {
	apply plugin: 'maven-publish'
	apply plugin: 'fabric-loom'
	apply plugin: 'net.minecrell.licenser'

	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	group = "net.fabricmc.fabric-api"

	dependencies {
		minecraft "com.mojang:minecraft:$Globals.mcVersion"
		mappings "net.fabricmc:yarn:${Globals.mcVersion}${Globals.yarnVersion}"
		modCompile "net.fabricmc:fabric-loader:0.4.8+build.154"
	}

	configurations {
		dev
	}

	repositories {
		mavenLocal()
	}

	jar {
		classifier = "dev"
	}

	afterEvaluate {
		remapJar {
			input = file("${project.buildDir}/libs/$archivesBaseName-${version}-dev.jar")
			archiveName = "${archivesBaseName}-${version}.jar"
		}

		artifacts {
			dev file: file("${project.buildDir}/libs/$archivesBaseName-${version}-dev.jar"), type: "jar", builtBy: jar
		}

		processResources {
			inputs.property "version", project.version

			from(sourceSets.main.resources.srcDirs) {
				include "fabric.mod.json"
				expand "version": project.version
			}

			from(sourceSets.main.resources.srcDirs) {
				exclude "fabric.mod.json"
			}
		}

		license {
			header rootProject.file('HEADER')
			include '**/*.java'
		}
	}

	task sourcesJar(type: Jar, dependsOn: classes) {
		classifier = 'sources'
		from sourceSets.main.allSource
	}
}

subprojects {
	task remapMavenJar(type: Copy, dependsOn: remapJar) {
		afterEvaluate {
			from("${project.buildDir}/libs/$archivesBaseName-${version}.jar")
			into("${project.buildDir}/libs/")
			rename { String fn -> "$archivesBaseName-${version}-maven.jar" }
		}
	}

	publishing {
		publications {
			mavenJava(MavenPublication) {
				artifact(file("${project.buildDir}/libs/$archivesBaseName-${version}-maven.jar")) {
					builtBy remapMavenJar
				}
				artifact(sourcesJar) {
					builtBy remapSourcesJar
				}
			}
		}

		repositories {
			maven {
				url "http://mavenupload.modmuss50.me/"
				if (project.hasProperty('mavenPass')) {
					credentials {
						username 'buildslave'
						password project.getProperty('mavenPass')
					}
				}
			}
		}
	}

}

task remapMavenJar(type: net.fabricmc.loom.task.RemapJarTask, dependsOn: jar) {
	afterEvaluate {
		input = file("${project.buildDir}/libs/${archivesBaseName}-${version}-dev.jar")
		archiveName = "${archivesBaseName}-${version}-maven.jar"
		addNestedDependencies = false
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact(file("${project.buildDir}/libs/$archivesBaseName-${version}-maven.jar")) {
				builtBy remapMavenJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
			pom.withXml {
				def depsNode = asNode().appendNode("dependencies")
				subprojects.each {
					def depNode = depsNode.appendNode("dependency")
					depNode.appendNode("groupId", it.group)
					depNode.appendNode("artifactId", it.name)
					depNode.appendNode("version", it.version)
					depNode.appendNode("scope", "compile")
				}
			}
		}	
	}

	repositories {
		maven {
			url "http://mavenupload.modmuss50.me/"
			if (project.hasProperty('mavenPass')) {
				credentials {
					username 'buildslave'
					password project.getProperty('mavenPass')
				}
			}
		}
	}
}

task licenseFormatAll
subprojects { p -> licenseFormatAll.dependsOn("${p.path}:licenseFormat") }
subprojects.each { remapJar.dependsOn("${it.path}:remapJar") }

dependencies {
	afterEvaluate {
		subprojects.each {
			compile project(path: ":${it.name}", configuration: "dev")
			include project("${it.name}:")
		}
	}
}

version = Globals.baseVersion + "+" + (ENV.BUILD_NUMBER ? ("build." + ENV.BUILD_NUMBER) : "local")

curseforge {
	if (project.hasProperty('curse_api_key')){
		apiKey = project.getProperty('curse_api_key')
	}
	project {
		id = '306612'
		changelog = 'A changelog can be found at https://github.com/FabricMC/fabric/commits/master'
		releaseType = 'alpha'
		addGameVersion '1.14-Snapshot'
		mainArtifact(file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar")) {
			displayName = "[$Globals.mcVersion] Fabric API $Globals.baseVersion build $ENV.BUILD_NUMBER"
		}
		afterEvaluate {
			uploadTask.dependsOn("remapJar")
		}
	}
	options{
		forgeGradleIntegration = false
	}
}

apply from: 'https://github.com/FabricMC/fabric-docs/raw/master/gradle/ideconfig.gradle'
