buildscript {
	repositories {
		jcenter()
		gradlePluginPortal()
		maven {
			name = 'Fabric'
			url = 'https://maven.fabricmc.net/'
		}
		mavenLocal()
	}
	dependencies {
		classpath "net.fabricmc:fabric-loom:0.2.1-SNAPSHOT"
	}
}

plugins {
	id 'com.matthewprenger.cursegradle' version "1.1.2"
}

def ENV = System.getenv()

def baseVersion = "0.2.7"
def mcVersion = "1.14 Pre-Release 5"

allprojects {
	repositories {
		mavenLocal()
	}

	apply plugin: 'java'
	apply plugin: 'eclipse'
	apply plugin: 'idea'

	apply plugin: net.fabricmc.loom.LoomGradlePlugin

	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	dependencies {
		minecraft "com.mojang:minecraft:$mcVersion"
		mappings "net.fabricmc:yarn:$mcVersion+build.2"
		modCompile "net.fabricmc:fabric-loader:0.4.1+build.127"
	}

	configurations {
		dev
	}

	afterEvaluate {
		artifacts {
			dev file: file("${project.buildDir}/libs/$archivesBaseName-$version-dev.jar"), type: "jar", builtBy: remapJar
		}
	}

	task sourcesJar(type: Jar, dependsOn: classes) {
		classifier = 'sources'
		from sourceSets.main.allSource
	}

	processResources {
		inputs.property "version", project.version

		from(sourceSets.main.resources.srcDirs) {
			include "fabric.mod.json"
			expand "version": project.version
		}

		from(sourceSets.main.resources.srcDirs) {
			exclude "fabric.mod.json"
		}
	}
}

dependencies {
	subprojects.each {
		compile project(path: ":${it.name}", configuration: "dev")
		include project("${it.name}:")
	}
}

archivesBaseName = "fabric"
version = baseVersion + "+" + (ENV.BUILD_NUMBER ? ("build." + ENV.BUILD_NUMBER) : "local")

curseforge {
	if (project.hasProperty('curse_api_key')){
		apiKey = project.getProperty('curse_api_key')
	}
	project {
		id = '306612'
		changelog = 'A changelog can be found at https://github.com/FabricMC/fabric/commits/master'
		releaseType = 'alpha'
		addGameVersion '1.14-Snapshot'
		mainArtifact(jar) {
			displayName = "[$mcVersion] Fabric API $baseVersion build $ENV.BUILD_NUMBER"
		}
	}
	options{
		forgeGradleIntegration = false
	}
}

apply from: 'https://github.com/FabricMC/fabric-docs/raw/master/gradle/maven.gradle'
apply from: 'https://github.com/FabricMC/fabric-docs/raw/master/gradle/license.gradle'
apply from: 'https://github.com/FabricMC/fabric-docs/raw/master/gradle/ideconfig.gradle'
