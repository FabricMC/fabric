buildscript {
	repositories {
		jcenter()
		gradlePluginPortal()
		maven {
			name = 'Fabric'
			url = 'https://maven.fabricmc.net/'
		}
	}
	dependencies {
		classpath "net.fabricmc:fabric-loom:0.2.0-SNAPSHOT"
	}
}

plugins {
	id 'com.matthewprenger.cursegradle' version "1.1.2"
}

def ENV = System.getenv()

def baseVersion = "0.2.6"
def mcVersion = "19w14b"

allprojects {
	apply plugin: 'java'
	apply plugin: 'eclipse'
	apply plugin: 'idea'

	apply plugin: net.fabricmc.loom.LoomGradlePlugin

	version = baseVersion + "." + (ENV.BUILD_NUMBER ?: "local")

	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	dependencies {
		minecraft "com.mojang:minecraft:$mcVersion"
		mappings "net.fabricmc:yarn:$mcVersion.1"
		modCompile "net.fabricmc:fabric-loader:0.3.7.109"
	}
}

subprojects {
	//Depend all of the sub projects on this root project
	dependencies {
		compile project(':')
	}
}


archivesBaseName = "fabric"

minecraft {
	refmapName = "net.fabricmc.fabric.refmap.json"
}

processResources {
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

curseforge {
	if (project.hasProperty('curse_api_key')){
		apiKey = project.getProperty('curse_api_key')
	}
	project {
		id = '306612'
		changelog = 'A changelog can be found at https://github.com/FabricMC/fabric/commits/master'
		releaseType = 'alpha'
		addGameVersion '1.14-Snapshot'
		mainArtifact(jar) {
			displayName = "[$mcVersion] Fabric API $baseVersion build $ENV.BUILD_NUMBER"
		}
	}
	options{
		forgeGradleIntegration = false
	}
}

apply from: 'https://github.com/FabricMC/fabric-docs/raw/master/gradle/maven.gradle'
apply from: 'https://github.com/FabricMC/fabric-docs/raw/master/gradle/license.gradle'
apply from: 'https://github.com/FabricMC/fabric-docs/raw/master/gradle/ideconfig.gradle'
