plugins {
	id 'java'
	id 'eclipse'
	id 'idea'
	id 'maven-publish'
	id 'fabric-loom' version '0.2.1-SNAPSHOT' apply false
	id 'com.matthewprenger.cursegradle' version "1.1.2"
}

def ENV = System.getenv()

def baseVersion = "0.3.0"
def mcVersion = "1.14 Pre-Release 5"

allprojects {
	apply plugin: 'maven-publish'
	apply plugin: 'fabric-loom'

	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	dependencies {
		minecraft "com.mojang:minecraft:$mcVersion"
		mappings "net.fabricmc:yarn:$mcVersion+build.4"
		modCompile "net.fabricmc:fabric-loader:0.4.1+build.129"
	}

	configurations {
		dev
	}

	repositories {
		mavenLocal()
	}

	// configure the maven publication
	publishing {
		publications {
			mavenJava(MavenPublication) {
				// add all the jars that should be included when publishing to maven
				artifact(jar) {
					builtBy remapJar
				}
				artifact(sourcesJar) {
					builtBy remapSourcesJar
				}
			}
		}	

		// select the repositories you want to publish to
		repositories {
			// uncomment to publish to the local maven
			// mavenLocal()
		}
	}

	afterEvaluate {
		artifacts {
			dev file: file("${project.buildDir}/libs/$archivesBaseName-$version-dev.jar"), type: "jar", builtBy: remapJar
		}

		processResources {
			inputs.property "version", project.version

			from(sourceSets.main.resources.srcDirs) {
				include "fabric.mod.json"
				expand "version": project.version
			}

			from(sourceSets.main.resources.srcDirs) {
				exclude "fabric.mod.json"
			}
		}
	}

	task sourcesJar(type: Jar, dependsOn: classes) {
		classifier = 'sources'
		from sourceSets.main.allSource
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			pom.withXml {
				def depsNode = asNode().appendNode("dependencies")
				subprojects.each {
					def depNode = depsNode.appendNode("dependency")
					depNode.appendNode("groupId", it.group)
					depNode.appendNode("artifactId", it.name)
					depNode.appendNode("version", it.version)
					depNode.appendNode("scope", "compile")
				}
			}
		}	
	}
}

dependencies {
	subprojects.each {
		compile project(path: ":${it.name}", configuration: "dev")
		include project("${it.name}:")
	}

	// workaround linux segfault	
	compile "org.lwjgl:lwjgl-jemalloc:3.2.1"
}

archivesBaseName = "fabric"
version = baseVersion + "+" + (ENV.BUILD_NUMBER ? ("build." + ENV.BUILD_NUMBER) : "local")

curseforge {
	if (project.hasProperty('curse_api_key')){
		apiKey = project.getProperty('curse_api_key')
	}
	project {
		id = '306612'
		changelog = 'A changelog can be found at https://github.com/FabricMC/fabric/commits/master'
		releaseType = 'alpha'
		addGameVersion '1.14-Snapshot'
		mainArtifact(jar) {
			displayName = "[$mcVersion] Fabric API $baseVersion build $ENV.BUILD_NUMBER"
		}
	}
	options{
		forgeGradleIntegration = false
	}
}

apply from: 'https://github.com/FabricMC/fabric-docs/raw/master/gradle/maven.gradle'
apply from: 'https://github.com/FabricMC/fabric-docs/raw/master/gradle/license.gradle'
apply from: 'https://github.com/FabricMC/fabric-docs/raw/master/gradle/ideconfig.gradle'
